apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.intel.realsense.camera"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 2
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def getD4xxFwVersion() {
    try {
        def stringsFile = file("./src/main/res/values/strings.xml")
        return new XmlParser().parse(stringsFile).string.find { it.@name.equals 'd4xx_fw_version' }.text()
    }catch(e){
        println(e)
        return "Unknown FW version"
    }
}

task downloadFirmware() {
    def fwVersion = getD4xxFwVersion()
    println("downloading fw: " + fwVersion)
    mkdir "$projectDir/src/main/res/raw"
    def f = new File("$projectDir/src/main/res/raw/fw_d4xx.bin")
    if (!f.exists()) {
        new URL('https://bintray.com/api/ui/download/intel-realsense/librealsense-dev/d4xx_fw/d4xx_fw-' + fwVersion + '.bin').withInputStream{ i -> f.withOutputStream{ it << i }}
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':librealsense')
    implementation 'com.android.support:design:26.0.0'
}
